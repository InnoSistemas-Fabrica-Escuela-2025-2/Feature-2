buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
	id 'io.github.jumosqu12.screenplayarchitecture' version '2.0.1'
	id 'java'
    id 'eclipse'
    id 'idea'
    id("net.serenity-bdd.serenity-gradle-plugin") version "4.3.2"
}

group 'co.edu.udea.certificacion'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

ext {
    serenityVersion = '4.1.0'
    serenityCucumberVersion = '4.1.0'
    lombokVersion = '1.18.22'
    junit = '4.13.2'
    hamcrest = '1.3'
    serenityWebVersion = '4.1.0'
}

dependencies {
    implementation "net.serenity-bdd:serenity-core:$rootProject.ext.serenityVersion"
    implementation "net.serenity-bdd:serenity-junit:$rootProject.ext.serenityVersion"
    implementation "net.serenity-bdd:serenity-screenplay:$rootProject.ext.serenityVersion"
    implementation "net.serenity-bdd:serenity-cucumber:$rootProject.ext.serenityCucumberVersion"
    implementation 'org.slf4j:slf4j-simple:2.0.5'
    implementation "net.serenity-bdd:serenity-screenplay-webdriver:$rootProject.ext.serenityWebVersion"

    //Test compilation
    testImplementation "junit:junit:$rootProject.ext.junit"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrest"
    implementation 'org.slf4j:slf4j-simple:2.0.5'
    testImplementation('io.github.bonigarcia:webdrivermanager:6.1.0')

    //Lombok dependencies
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor  "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly  "org.projectlombok:lombok:${lombokVersion}"

    //Additional library
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.12.0'

}

// Run Serenity aggregate report even if tests fail, and publish the HTML report
tasks.named('test') {
    // Do not fail the build on test failures so the Serenity report is still generated
    ignoreFailures = true
    finalizedBy 'aggregate'
}

// Task alias to generate the Serenity report on demand
tasks.register('e2eReport') {
    group = 'verification'
    description = 'Generates Serenity aggregate report for E2E tests into qa/reports/e2e'
    dependsOn 'aggregate'
}

// After aggregation, copy the Serenity site to qa/reports/e2e for a stable location
tasks.named('aggregate') {
    finalizedBy 'publishE2EReport'
}

tasks.register('publishE2EReport', Copy) {
    group = 'verification'
    description = 'Copies Serenity HTML report to qa/reports/e2e'
    // Serenity Gradle plugin publishes the site under target/site/serenity
    from file("${projectDir}/target/site/serenity")
    into file("${projectDir}/../reports/e2e")
}
